#!/usr/bin/env python

Import("env")

common_haiku = [
    'audio_driver_media_kit.cpp',
    'context_gl_haiku.cpp',
    'dir_access_haiku.cpp',
    'haiku_application.cpp',
    'haiku_gl_view.cpp',
    'haiku_window.cpp',
    'key_mapping_haiku.cpp',
    'midi_driver_midi2_kit.cpp',
    'os_haiku.cpp',
    'thread_haiku.cpp'
]

target = env.add_program("#bin/godot", ["godot_haiku.cpp"] + common_haiku)

command = env.Command("#bin/godot.rsrc", "#platform/haiku/godot.rdef", ["rc -o $TARGET $SOURCE"])


def addResourcesAction(target=None, source=None, env=None):
    return env.Execute("xres -o " + File(target)[0].path + " bin/godot.rsrc")


env.AddPostAction(target, addResourcesAction)
env.Depends(target, command)
